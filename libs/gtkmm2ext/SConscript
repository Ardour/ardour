# -*- python -*-

import os
import os.path
import glob

Import('env final_prefix install_prefix libraries i18n')

gtkmm2ext = env.Copy()
gtkmm2ext.Merge ([ 
    libraries['sigc2'], 
    libraries['pbd'],
    libraries['gtk2'],
    libraries['glibmm2'],
    libraries['pangomm'],
    libraries['atkmm'],
    libraries['gdkmm2'],
    libraries['gtkmm2']
    ])

#
# this defines the version number of libgtkmm2ext
# 

domain = 'libgtkmm2ext'

gtkmm2ext.Append(DOMAIN=domain,MAJOR=0,MINOR=8,MICRO=3)
gtkmm2ext.Append(CXXFLAGS="-DPACKAGE=\\\"" + domain + "\\\"")
gtkmm2ext.Append(CXXFLAGS="-DLIBSIGC_DISABLE_DEPRECATED")
#gtkmm2ext.Append(CPPPATH='#libs/surfaces/control_protocol')
gtkmm2ext.Append(PACKAGE=domain)
gtkmm2ext.Append(POTFILE=domain + '.pot')

extra_sources = []

gtkmm2ext_files = Split("""
auto_spin.cc
barcontroller.cc
binding_proxy.cc
choice.cc
click_box.cc
dndtreeview.cc
fastmeter.cc
focus_entry.cc
grouped_buttons.cc
gtk_ui.cc
hexentry.cc
idle_adjustment.cc
pathlist.cc
pixfader.cc
pixscroller.cc
popup.cc
prompter.cc
selector.cc
slider_controller.cc
stateful_button.cc
tearoff.cc
textviewer.cc
utils.cc
version.cc
window_title.cc
""")

gtkosx_files=Split("""
sync-menu.c
""")

if gtkmm2ext['GTKOSX']:
    extra_sources += gtkosx_files
    gtkmm2ext.Append (CCFLAGS="-DTOP_MENUBAR -DGTKOSX")
    gtkmm2ext.Append (LINKFLAGS="-framework Carbon")

gtkmm2ext.VersionBuild(['version.cc','gtkmm2ext/version.h'], [])

gtkmm2ext.Append(CCFLAGS="-D_REENTRANT")
gtkmm2ext.Append(CCFLAGS="-DLOCALEDIR=\\\""+final_prefix+"/share/locale\\\"")

libgtkmm2ext = gtkmm2ext.SharedLibrary('gtkmm2ext', gtkmm2ext_files + extra_sources)

Default(libgtkmm2ext)

if env['NLS']:
    i18n (gtkmm2ext, gtkmm2ext_files, env)

env.Alias('install', env.Install(os.path.join(install_prefix, env['LIBDIR'], 'ardour2'), libgtkmm2ext))

env.Alias('tarball', env.Distribute (env['DISTTREE'],
                                     [ 'SConscript', 'i18n.h', 'gettext.h'] +
                                     gtkmm2ext_files +
				     gtkosx_files + 
                                     glob.glob('po/*.po') +
                                     glob.glob('gtkmm2ext/*.h')))
