# -*- python -*-

import os
import os.path
import glob

gnomecanvasmm_files = glob.glob('libgnomecanvasmm/*.cc')

Import('env libraries install_prefix')
gnomecanvasmm = env.Clone()
gnomecanvasmm.Merge([libraries['glibmm2'],
                     libraries['gtk2'],
                     libraries['sigc2'],
                     libraries['pangomm'],
                     libraries['atkmm'],
                     libraries['gdkmm2'],
                     libraries['gtkmm2'],
                     libraries['libgnomecanvas2'] ])

if gnomecanvasmm['IS_OSX']:
	gnomecanvasmm.Append (LINKFLAGS="-Xlinker -headerpad -Xlinker 2048")

libgnomecanvasmm = gnomecanvasmm.SharedLibrary('libgnomecanvasmm', gnomecanvasmm_files)
Default(libgnomecanvasmm)

env.Alias('install', env.Install(os.path.join(install_prefix, env['LIBDIR'], 'ardour2'), libgnomecanvasmm))

env.Alias('tarball', env.Distribute (env['DISTTREE'],
                                     [ 'SConscript', 'libgnomecanvasmmconfig.h', 'libgnomecanvasmm.h'] +
				     gnomecanvasmm_files + 
				     glob.glob('libgnomecanvasmm/*.h') +
				     glob.glob('libgnomecanvasmm/private/*.h')
				     ))
