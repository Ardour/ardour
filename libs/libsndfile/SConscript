# -*- python -*-

import os
import os.path
import glob

sndfile_files = glob.glob('src/*.c') + glob.glob('src/GSM610/*.c') + glob.glob('src/G72x/*.c')

Import('env install_prefix libraries use_flac')
sndfile = env.Copy()
sndfile.Merge([libraries['flac'] ])

domain = 'libsndfile'

sndfile.Append(CCFLAGS = "-DPACKAGE=\\\"" + domain + "\\\"")
sndfile.Append(CCFLAGS = "-DVERSION=\\\"ardour-special\\\"")
# mingw may need this
#sndfile.Append(CCFLAGS="-no-undefined")
sndfile.Append(PACKAGE = domain)
sndfile.Append(POTFILE = domain + '.pot')

conf = Configure(sndfile)

if conf.CheckCHeader('/System/Library/Frameworks/CoreServices.framework/Headers/CoreServices.h'):
    sndfile.Append(LINKFLAGS = "-framework CoreServices")

sndfile = conf.Finish()

libsndfile = sndfile.SharedLibrary('sndfile-ardour', sndfile_files)

if use_flac:
       sndfile_h = sndfile.Command('src/sndfile.h', ['src/sndfile.h.in'], 'cd libs/libsndfile && ./configure && cd -', ENV=os.environ)
else:
       sndfile_h = sndfile.Command('src/sndfile.h', ['src/sndfile.h.in'], 'cd libs/libsndfile && ./configure --disable-flac && cd -', ENV=os.environ)

Default([sndfile_h,libsndfile])

env.Alias('install', env.Install(os.path.join(install_prefix, env['LIBDIR'], 'ardour2'), libsndfile))

env.Alias('tarball', env.Distribute (env['DISTTREE'],
                                     [ 'NEWS', 'README', 'AUTHORS', 'ChangeLog',
                                       'configure', 'SConscript',
                                       'install-sh',
                                       'libsndfile.spec.in',
                                       'sndfile.pc.in',
                                       'config.sub',
                                       'config.guess'
                                       ] +
                                       sndfile_files +
                                       glob.glob('src/*.h') +
                                       glob.glob('src/GSM610/*.h') +
                                       glob.glob('src/G72x/*.h') +
                                      [ 'src/sndfile.h.in', 'src/config.h.in', 'src/Symbols.linux', 'src/Symbols.darwin', 'src/libsndfile.def', 'src/cygsndfile.def' ]
                                     ))
                                     
