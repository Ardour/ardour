# -*- python -*-

import glob

Import('env libraries')

pbd3 = env.Copy()

domain = 'libpbd'
pbd3.Append(DOMAIN=domain,MAJOR=3,MINOR=2,MICRO=0)

pbd3_files = Split("""
basename.cc
dirname.cc
dmalloc.cc
mountpoint.cc
pathscanner.cc
pool.cc
pthread_utils.cc
receiver.cc
strsplit.cc
textreceiver.cc
transmitter.cc
undo.cc
unescape.cc
version.cc
whitespace.cc
xml++.cc
""")

conf = Configure(pbd3)
if conf.CheckFunc('getmntent'):
    conf.env.Append(CCFLAGS="-DHAVE_GETMNTENT")
pbd3 = conf.Finish()

pbd3.Merge ([ libraries['sigc2'], libraries['xml'] ])

pbd3.Append(CCFLAGS="-D_REENTRANT -D_LARGEFILE_SOURCE -D_LARGEFILE64_SOURCE")
pbd3.Append(CCFLAGS="-DLIBSIGC_DISABLE_DEPRECATED")

pbd3.VersionBuild(['version.cc','pbd/version.h'], 'SConscript')

libpbd3 = pbd3.StaticLibrary('pbd', pbd3_files)
Default(libpbd3)

env.Alias('tarball', env.Distribute (env['DISTTREE'],
                                     [ 'SConscript' ] + pbd3_files + glob.glob('pbd/*.h')))
