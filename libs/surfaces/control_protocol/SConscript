# -*- python -*-

import os
import os.path
import glob

Import('env final_prefix install_prefix final_config_prefix libraries i18n')

cp = env.Clone()

#
# this defines the version number of libardour_cp
# 

domain = 'ardour_cp'

cp.Append(DOMAIN = domain, MAJOR = 1, MINOR = 0, MICRO = 0)
cp.Append(CXXFLAGS = "-DPACKAGE=\\\"" + domain + "\\\"")
cp.Append(CXXFLAGS="-DLIBSIGC_DISABLE_DEPRECATED")
cp.Append(PACKAGE = domain)
cp.Append(POTFILE = domain + '.pot')

cp_files=Split("""
basic_ui.cc
control_protocol.cc
smpte.cc
""")

cp.Append(CCFLAGS="-D_REENTRANT -D_LARGEFILE_SOURCE -D_LARGEFILE64_SOURCE")
cp.Append(CXXFLAGS="-DDATA_DIR=\\\""+final_prefix+"/share\\\"")
cp.Append(CXXFLAGS="-DCONFIG_DIR=\\\""+final_config_prefix+"\\\"")
cp.Append(CXXFLAGS="-DLOCALEDIR=\\\""+final_prefix+"/share/locale\\\"")
cp.Append(CPPPATH = libraries['jack'].get('CPPPATH', []))

cp.Merge ([
    libraries['ardour'],
    libraries['sndfile'],
    libraries['sigc2'],
    libraries['pbd'],
    libraries['midi++2'],
    libraries['xml'],
    libraries['glib2'],
    libraries['glibmm2'],
    libraries['boost']
	])

libardour_cp = cp.SharedLibrary('ardour_cp', cp_files)

Default(libardour_cp)

if env['NLS']:
	i18n (cp, cp_files, env)
	      
env.Alias('install', env.Install(os.path.join(install_prefix, env['LIBDIR'], 'ardour2'), libardour_cp))

env.Alias('tarball', env.Distribute (env['DISTTREE'],
				     [ 'SConscript' ] +
				     cp_files + 
				     glob.glob('po/*.po') + glob.glob('*.h') + glob.glob('control_protocol/*.h')))
