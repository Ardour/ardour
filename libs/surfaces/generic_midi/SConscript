# -*- python -*-

import os
import os.path
import glob

Import('env final_prefix install_prefix final_config_prefix libraries i18n')

genericmidi = env.Clone()

#
# this defines the version number of libardour_genericmidi
# 

domain = 'ardour_genericmidi'

genericmidi.Append(DOMAIN = domain, MAJOR = 1, MINOR = 0, MICRO = 0)
genericmidi.Append(CXXFLAGS = "-DPACKAGE=\\\"" + domain + "\\\"")
genericmidi.Append(CXXFLAGS="-DLIBSIGC_DISABLE_DEPRECATED")
genericmidi.Append(PACKAGE = domain)
genericmidi.Append(POTFILE = domain + '.pot')

genericmidi_files=Split("""
interface.cc
generic_midi_control_protocol.cc
midicontrollable.cc
""")

genericmidi.Append(CCFLAGS="-D_REENTRANT -D_LARGEFILE_SOURCE -D_LARGEFILE64_SOURCE")
genericmidi.Append(CXXFLAGS="-DDATA_DIR=\\\""+final_prefix+"/share\\\"")
genericmidi.Append(CXXFLAGS="-DCONFIG_DIR=\\\""+final_config_prefix+"\\\"")
genericmidi.Append(CXXFLAGS="-DLOCALEDIR=\\\""+final_prefix+"/share/locale\\\"")
genericmidi.Append(CPPPATH = libraries['jack'].get('CPPPATH', []))

genericmidi.Merge ([
	libraries['ardour'],
	libraries['ardour_cp'],
	libraries['sndfile'],
	libraries['midi++2'],
	libraries['pbd'],
	libraries['sigc2'],
	libraries['usb'],
	libraries['xml'],
	libraries['glib2'],
	libraries['glibmm2'],
	libraries['boost']
	])

libardour_genericmidi = genericmidi.SharedLibrary('ardour_genericmidi', genericmidi_files)

Default(libardour_genericmidi)

if env['NLS']:
	i18n (genericmidi, genericmidi_files, env)
	      
env.Alias('install', env.Install(os.path.join(install_prefix, env['LIBDIR'], 'ardour2', 'surfaces'), libardour_genericmidi))

env.Alias('tarball', env.Distribute (env['DISTTREE'],
				     [ 'SConscript' ] +
				     genericmidi_files + 
				     glob.glob('po/*.po') + glob.glob('*.h')))
