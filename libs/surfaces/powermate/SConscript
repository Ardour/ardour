# -*- python -*-

import os
import os.path
import glob

Import('env final_prefix install_prefix final_config_prefix libraries i18n')

powermate = env.Clone()

#
# this defines the version number of powermate
# 

domain = 'ardour_powermate'

powermate.Append(DOMAIN = domain, MAJOR = 1, MINOR = 0, MICRO = 0)
powermate.Append(CXXFLAGS = "-DPACKAGE=\\\"" + domain + "\\\"")
powermate.Append(CXXFLAGS="-DLIBSIGC_DISABLE_DEPRECATED")
powermate.Append(PACKAGE = domain)
powermate.Append(POTFILE = domain + '.pot')

powermate_files=Split("""
interface.cc
powermate.cc
""")

powermate.Append(CCFLAGS="-D_REENTRANT -D_LARGEFILE_SOURCE -D_LARGEFILE64_SOURCE")
powermate.Append(CXXFLAGS="-DDATA_DIR=\\\""+final_prefix+"/share\\\"")
powermate.Append(CXXFLAGS="-DCONFIG_DIR=\\\""+final_config_prefix+"\\\"")
powermate.Append(CXXFLAGS="-DLOCALEDIR=\\\""+final_prefix+"/share/locale\\\"")

powermate.Merge ([
    libraries['ardour'],
    libraries['ardour_cp'],
    libraries['sigc2'],
    libraries['pbd'],
    libraries['midi++2'],
    libraries['xml'],
    libraries['usb'],
    libraries['glib2'],
    libraries['glibmm2']
    ])

libardour_powermate = powermate.SharedLibrary('ardour_powermate', powermate_files)

if powermate['POWERMATE']:
    Default(libardour_powermate)

    if env['NLS']:
	    i18n (powermate, powermate_files, env)
	      
    env.Alias('install', env.Install(os.path.join(install_prefix, env['LIBDIR'], "ardour2", "surfaces"), libardour_powermate))

env.Alias('tarball', env.Distribute (env['DISTTREE'],
				     [ 'SConscript' ] +
				     powermate_files + 
				     glob.glob('po/*.po') + glob.glob('*.h')))
