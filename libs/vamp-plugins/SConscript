# -*- python -*-

import os
import os.path
import glob

plugin_files = Split("""
plugins.cpp
AmplitudeFollower.cpp
PercussionOnsetDetector.cpp
SpectralCentroid.cpp
ZeroCrossing.cpp
""")

aubio_files = Split ("""
Onset.cpp
""")

Import('env install_prefix libraries')
vampplugs = env.Copy()

vampplugs.Append (CPPATH='#libs/vamp-sdk/vamp', CXXFLAGS="-Ilibs/vamp-sdk")
vampplugs.Merge ([libraries['vamp'],
                  libraries['vamphost']
                  ])

sources = plugin_files

if vampplugs['AUBIO']:
    sources += aubio_files
    vampplugs.Merge ([libraries['aubio']])

libvampplugins = vampplugs.SharedLibrary('ardourvampplugins', sources)

Default(libvampplugins)

env.Alias('install', env.Install(os.path.join(install_prefix, env['LIBDIR'], 'ardour2', 'vamp'), libvampplugins))

env.Alias('tarball', env.Distribute (env['DISTTREE'],
                                     [ 'SConscript' ] +
                                     plugin_files + aubio_files +
                                     glob.glob('*.h')))
